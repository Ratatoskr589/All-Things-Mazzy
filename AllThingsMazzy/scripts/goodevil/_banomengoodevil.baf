IF	//Trigger for Anomen leaving the party forever.
	Global("_bAnoLeavesForever","GLOBAL",1)
THEN
	RESPONSE #100
	SetGlobal("KickedOut","LOCALS",2)
	GivePartyAllEquipment()
	LeaveParty() 
	EscapeArea()
END

//ClaraSex scene trigger - if had sex already
IF
	Global("_bSleepsWithClara","GLOBAL",4) 
	Global("_bSWCAn1","GLOBAL",0)
	IfValidForPartyDialog("Anomen")
	Global("AnomenRomanceActive","GLOBAL",2)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//ClaraSex scene trigger - if haven't had sex
IF
	Global("_bSleepsWithClara","GLOBAL",4)
	Global("_bSWCAn2","GLOBAL",0)	
	IfValidForPartyDialog("Anomen")
	Global("AnomenMatch","GLOBAL",1) 
	!Global("AnomenRomanceActive","GLOBAL",2)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Anomen Romance Fix
IF	Global("AnomenMatch","GLOBAL",1)
	InParty("Anomen") 
	Global("AnomenRomanceActive","GLOBAL",2)
	!Global("_bAnomenRomanceActive","GLOBAL",2)
THEN 
	RESPONSE #100
	SetGlobal("_bAnomenRomanceActive","GLOBAL",2)
END

IF	Global("AnomenMatch","GLOBAL",1)
	IfValidForPartyDialog("Anomen")
	CombatCounter(0)
	Global("AnomenRomanceActive","GLOBAL",3)
	Global("_bAnomenRomanceFix","GLOBAL",0)
THEN 
	RESPONSE #100
	SetGlobal("_bAnomenRomanceFix","GLOBAL",1)
END

IF
	IfValidForPartyDialog("Anomen")
	CombatCounter(0)
	Global("_bAnomenRomanceFix","GLOBAL",1)
THEN
	RESPONSE #100
	SetGlobalTimer("_bAnomenFixTimer","GLOBAL",ONE_HOUR)
	SetGlobal("_bAnomenRomanceFix","GLOBAL",2)
END

IF
	InParty("Anomen") 
	!StateCheck("Anomen",CD_STATE_NOTVALID)
	CombatCounter(0)
	Global("_bAnomenRomanceFix","GLOBAL",2)
	GlobalTimerExpired("_bAnomenFixTimer","GLOBAL")
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Clara-Anomen Talk Timer (all talks w/ same timer). Shortened to be possible before test
IF
	IfValidForPartyDialog("Anomen")
	IfValidForPartyDialog("_bClara")
	!Global("AnomenRomanceActive","GLOBAL",2)
	!Global("AnomenRomanceActive","GLOBAL",1)
	OR(4)
	Global("_bClaraAnomenLove","GLOBAL",0)
	Global("_bClaraAnomenLove","GLOBAL",3)
	Global("_bClaraAnomenLove","GLOBAL",12)
	Global("_bClaraAnomenLove","GLOBAL",15)
THEN 
	RESPONSE #100
	SetGlobalTimer("_bCALoveTime","GLOBAL",TWO_DAYS)
	IncrementGlobal("_bClaraAnomenLove","GLOBAL",1)
END

IF	
	IfValidForPartyDialog("Anomen")
	IfValidForPartyDialog("_bClara")
	GlobalTimerExpired("_bCALoveTime","GLOBAL")
	!Global("AnomenRomanceActive","GLOBAL",2)
	!Global("AnomenRomanceActive","GLOBAL",1)
	OR(4)
	Global("_bClaraAnomenLove","GLOBAL",1)
	Global("_bClaraAnomenLove","GLOBAL",4)
	Global("_bClaraAnomenLove","GLOBAL",13)
	Global("_bClaraAnomenLove","GLOBAL",16)
	CombatCounter(0)
THEN 
	RESPONSE #100
	IncrementGlobal("_bClaraAnomenLove","GLOBAL",1)
END

IF
	IfValidForPartyDialog("Anomen")
	IfValidForPartyDialog("_bClara")
	!Global("AnomenRomanceActive","GLOBAL",2)
	!Global("AnomenRomanceActive","GLOBAL",1)
	OR(4)
	Global("_bClaraAnomenLove","GLOBAL",2)
	Global("_bClaraAnomenLove","GLOBAL",5)
	Global("_bClaraAnomenLove","GLOBAL",14)
	Global("_bClaraAnomenLove","GLOBAL",17)
	CombatCounter(0)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySong(35)
	StartDialogNoSet(Player1)
END

//4th & 7th Talk - different timer
IF
	IfValidForPartyDialog("Anomen")
	IfValidForPartyDialog("_bClara")
	!Global("AnomenRomanceActive","GLOBAL",2)
	!Global("AnomenRomanceActive","GLOBAL",1)
	OR(2)
	Global("_bClaraAnomenLove","GLOBAL",18)
	Global("_bClaraAnomenLove","GLOBAL",9)
THEN 
	RESPONSE #100
	SetGlobalTimer("_bCALoveTime2","GLOBAL",ONE_DAY)
	IncrementGlobal("_bClaraAnomenLove","GLOBAL",1)
END

IF	
	IfValidForPartyDialog("Anomen")
	IfValidForPartyDialog("_bClara")
	!Global("AnomenRomanceActive","GLOBAL",2)
	!Global("AnomenRomanceActive","GLOBAL",1)
	GlobalTimerExpired("_bCALoveTime2","GLOBAL")
	OR(2)
	Global("_bClaraAnomenLove","GLOBAL",19)
	Global("_bClaraAnomenLove","GLOBAL",10)
	CombatCounter(0)
THEN 
	RESPONSE #100
	IncrementGlobal("_bClaraAnomenLove","GLOBAL",1)
END

IF
	IfValidForPartyDialog("Anomen")
	IfValidForPartyDialog("_bClara")
	!Global("AnomenRomanceActive","GLOBAL",2)
	!Global("AnomenRomanceActive","GLOBAL",1)
	OR(2)
	Global("_bClaraAnomenLove","GLOBAL",20)
	Global("_bClaraAnomenLove","GLOBAL",11)
	CombatCounter(0)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySong(35)
	StartDialogNoSet(Player1)
END

//8th Talk needs own timer so it doesn't try to play too early
IF
	IfValidForPartyDialog("Anomen")
	IfValidForPartyDialog("_bClara")
	!Global("AnomenRomanceActive","GLOBAL",2)
	!Global("AnomenRomanceActive","GLOBAL",1)
	Global("_bClaraAnomenLove","GLOBAL",21)
THEN 
	RESPONSE #100
	SetGlobalTimer("_bCALoveTime","GLOBAL",TWO_DAYS)
	IncrementGlobal("_bClaraAnomenLove","GLOBAL",1)
END

IF	
	IfValidForPartyDialog("Anomen")
	IfValidForPartyDialog("_bClara")
	!Global("AnomenRomanceActive","GLOBAL",2)
	!Global("AnomenRomanceActive","GLOBAL",1)
	GlobalTimerExpired("_bCALoveTime","GLOBAL")
	Global("_bClaraAnomenLove","GLOBAL",22)
THEN 
	RESPONSE #100
	IncrementGlobal("_bClaraAnomenLove","GLOBAL",1)
END

IF//killed Saerk version
	IfValidForPartyDialog("Anomen")
	IfValidForPartyDialog("_bClara")
	!Global("AnomenRomanceActive","GLOBAL",2)
	!Global("AnomenRomanceActive","GLOBAL",1)
	Global("_bClaraAnomenLove","GLOBAL",23)
	Global("SaerkRevengeDone","LOCALS",1) //This gets set when Anomen talks after Saerk's death
	!Global("AnomenDecide","GLOBAL",4)
	CombatCounter(0)
THEN
	RESPONSE #100
	Wait(10)
	PlaySong(0)
	PlaySong(35)
	StartDialogNoSet(Player1)
END

IF//didn't kill Saerk version
	IfValidForPartyDialog("Anomen")
	IfValidForPartyDialog("_bClara")
	!Global("AnomenRomanceActive","GLOBAL",2)
	!Global("AnomenRomanceActive","GLOBAL",1)
	Global("_bClaraAnomenLove","GLOBAL",23)
	Global("AnomenDecide","GLOBAL",4)
	CombatCounter(0)
THEN
	RESPONSE #100
	Wait(5)
	PlaySong(0)
	PlaySong(35)
	StartDialogNoSet(Player1)
END

//Timer for Talks 9 & 10. Require Anomen not having taken test
IF
	IfValidForPartyDialog("Anomen")
	IfValidForPartyDialog("_bClara")
	!Global("AnomenRomanceActive","GLOBAL",2)
	!Global("AnomenRomanceActive","GLOBAL",1)
	GlobalGT("_bClaraAnomenLove","GLOBAL",23)
	Global("_bAnomenClaraRomance","GLOBAL",1)
	Global("AnomenIsNotKnight","GLOBAL",0) 
	Global("AnomenIsKnight","GLOBAL",0)
	OR(2)
	Global("_bACTestTalk","GLOBAL",0)
	Global("_bACTestTalk","GLOBAL",3)
THEN 
	RESPONSE #100
	SetGlobalTimer("_bCALoveTime2","GLOBAL",ONE_DAY)
	IncrementGlobal("_bACTestTalk","GLOBAL",1)
END

IF	
	IfValidForPartyDialog("Anomen")
	IfValidForPartyDialog("_bClara")
	GlobalTimerExpired("_bCALoveTime2","GLOBAL")
	!Global("AnomenRomanceActive","GLOBAL",2)
	!Global("AnomenRomanceActive","GLOBAL",1)
	Global("AnomenIsNotKnight","GLOBAL",0)
	Global("AnomenIsKnight","GLOBAL",0)
	OR(2)
	Global("_bACTestTalk","GLOBAL",1)
	Global("_bACTestTalk","GLOBAL",4)
THEN 
	RESPONSE #100
	IncrementGlobal("_bACTestTalk","GLOBAL",1)
END

IF
	IfValidForPartyDialog("Anomen")
	IfValidForPartyDialog("_bClara")
	!Global("AnomenRomanceActive","GLOBAL",2)
	!Global("AnomenRomanceActive","GLOBAL",1)
	Global("AnomenIsNotKnight","GLOBAL",0) 
	Global("AnomenIsKnight","GLOBAL",0)
	OR(2)
	Global("_bACTestTalk","GLOBAL",2)
	Global("_bACTestTalk","GLOBAL",5)
	CombatCounter(0)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySong(35)
	StartDialogNoSet(Player1)
END

//Timer for Post Test Failure - has to not be a knight
IF
	IfValidForPartyDialog("Anomen")
	IfValidForPartyDialog("_bClara")
	!Global("AnomenRomanceActive","GLOBAL",2)
	!Global("AnomenRomanceActive","GLOBAL",1)
	Global("_bAnomenClaraRomance","GLOBAL",1)
	Global("AnomenIsNotKnight","GLOBAL",1)
	OR(5)
	Global("_bClaraAnomenLove","GLOBAL",24)
	Global("_bClaraAnomenLove","GLOBAL",27)
	Global("_bClaraAnomenLove","GLOBAL",30)
	Global("_bClaraAnomenLove","GLOBAL",33)
	Global("_bClaraAnomenLove","GLOBAL",36)
THEN 
	RESPONSE #100
	SetGlobalTimer("_bCALoveTime","GLOBAL",TWO_DAYS)
	IncrementGlobal("_bClaraAnomenLove","GLOBAL",1)
END

IF	//some of these are in d script instead
	IfValidForPartyDialog("Anomen")
	IfValidForPartyDialog("_bClara")
	GlobalTimerExpired("_bCALoveTime","GLOBAL")
	OR(4)
	Global("_bClaraAnomenLove","GLOBAL",25)
	Global("_bClaraAnomenLove","GLOBAL",28)
	Global("_bClaraAnomenLove","GLOBAL",34)
	Global("_bClaraAnomenLove","GLOBAL",37)
THEN 
	RESPONSE #100
	IncrementGlobal("_bClaraAnomenLove","GLOBAL",1)
END

IF
	IfValidForPartyDialog("Anomen")
	IfValidForPartyDialog("_bClara")
	!Global("AnomenRomanceActive","GLOBAL",2)
	!Global("AnomenRomanceActive","GLOBAL",1)
	Global("AnomenIsNotKnight","GLOBAL",1)
	OR(4)
	Global("_bClaraAnomenLove","GLOBAL",26)
	Global("_bClaraAnomenLove","GLOBAL",29)
	Global("_bClaraAnomenLove","GLOBAL",32)
	Global("_bClaraAnomenLove","GLOBAL",35)
	CombatCounter(0)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySong(35)
	StartDialogNoSet(Player1)
END

//Trigger for proposal talk, has to be nightime
IF
	IfValidForPartyDialog("Anomen")
	IfValidForPartyDialog("_bClara")
	!Global("AnomenRomanceActive","GLOBAL",2)
	!Global("AnomenRomanceActive","GLOBAL",1)
	Global("AnomenIsNotKnight","GLOBAL",1)
	Global("_bClaraAnomenLove","GLOBAL",38)
	!AreaType(DUNGEON)
	CombatCounter(0)
	OR(2)
	TimeOfDay(DUSK)
	TimeOfDay(NIGHT)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySong(35)
	StartDialogNoSet(Player1)
END

//Post Proposal Talks
IF
	IfValidForPartyDialog("Anomen")
	IfValidForPartyDialog("_bClara")
	!Global("AnomenRomanceActive","GLOBAL",2)
	!Global("AnomenRomanceActive","GLOBAL",1)
	Global("_bAnomenClaraRomance","GLOBAL",2)
	Global("AnomenIsNotKnight","GLOBAL",1)
	OR(2)
	Global("_bClaraAnomenLove","GLOBAL",39)
	Global("_bClaraAnomenLove","GLOBAL",42)
THEN 
	RESPONSE #100
	SetGlobalTimer("_bCALoveTime","GLOBAL",TWO_DAYS)
	IncrementGlobal("_bClaraAnomenLove","GLOBAL",1)
END

IF	//one of these is in d script instead
	IfValidForPartyDialog("Anomen")
	IfValidForPartyDialog("_bClara")
	!Global("AnomenRomanceActive","GLOBAL",2)
	!Global("AnomenRomanceActive","GLOBAL",1)
	GlobalTimerExpired("_bCALoveTime","GLOBAL")
	Global("_bClaraAnomenLove","GLOBAL",40)
THEN 
	RESPONSE #100
	IncrementGlobal("_bClaraAnomenLove","GLOBAL",1)
END

IF
	IfValidForPartyDialog("Anomen")
	IfValidForPartyDialog("_bClara")
	!Global("AnomenRomanceActive","GLOBAL",2)
	!Global("AnomenRomanceActive","GLOBAL",1)
	Global("AnomenIsNotKnight","GLOBAL",1)
	OR(2)
	Global("_bClaraAnomenLove","GLOBAL",41)
	Global("_bClaraAnomenLove","GLOBAL",44)
	CombatCounter(0)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySong(35)
	StartDialogNoSet(Player1)
END

//Script to make Terl show up in our version
IF
	IfValidForPartyDialog("Anomen")
	IfValidForPartyDialog("_bClara")
	GlobalTimerExpired("SpawnTerl","GLOBAL")  //Borrowed from standard code. If we ever have issues, make unique timer.
	!Global("Chapter","GLOBAL",%bg2_chapter_4%) //Chapter 4 (this is the code the buffer evaluates)
	!Global("Chapter","GLOBAL",%bg2_chapter_5%)  //Chapter 5 (Underdark)
	//!Global("Chapter","GLOBAL",%bg2_chapter_7%)  //Chapter 7  (Suldanesselar)
	//!AreaCheck("AR1900")  //Druid Grove Area
	//!AreaCheck("AR2500")   //Elven Temple at Underdark
	//!AreaCheck("AR2600")   //Thetyr Wood
	AreaType(OUTDOOR)
	!Exists("Terl")  // Terl
	CombatCounter(0)
	!AreaCheck("AR0041")   //Random Encounter
	!AreaCheck("AR0042")   //Random Encounter
	!AreaCheck("AR0043")   //Random Encounter
	!AreaCheck("AR0044")   //Random Encounter
	!AreaCheck("AR0045")   //Random Encounter
	!AreaCheck("AR0046")   //Random Encounter
	!AreaCheck("AR2601")   //Meet Drizzt
	!AreaCheck("AR2700")   //Ancient Spirits (mod only)
	!AreaCheck("AR2800")   //Suldanessellar
	!AreaCheck("AR2806")   //Tree of Life
	!AreaCheck("AR2807")   //Black Dragon Hideout	
	!AreaCheck("OH6010")   //Neera random encounter
	//!AreaCheck("OH6000")   //Wild Forest
	//!AreaCheck("OH6100")   //Hidden Refuge
	//!AreaCheck("OH6200")    //Hidden Refuge Destroyed
	!AreaCheck("OH7310")   //Hexxat - L Scene
	//!AreaCheck("OH5100")    //Resurrection Gorge
	//!AreaCheck("OH5200")    //Resurrection Gorge Interior
	//!AreaCheck("OH5300")    //Helmite Camp
	//!AreaCheck("OH4000")    //Rasaad Amphitheater
	//!AreaCheck("OH4100")    //Rasaad Dark Moon Heretic Temple
	Global("_bAnomenClaraRomance","GLOBAL",2)
	Global("_bClaraAnomenLove","GLOBAL",45)
	!Global("AnomenRomanceActive","GLOBAL",2) //Still can't be romancing Anomen
	!Global("AnomenRomanceActive","GLOBAL",1)
	Global("RomancePartFour","GLOBAL",0)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		CreateCreatureObjectEffect("bdoffscr","",Myself)  // No such index
		ApplySpellRES("bdoffscr","bdoffscr")  // No such index
		SmallWait(1)
		CreateCreatureObjectEffect("TERL","","bdoffscr")  // Terl
		SetInterrupt(TRUE)
END

IF
	IfValidForPartyDialog("Anomen")
	IfValidForPartyDialog("_bClara")
	GlobalTimerExpired("SpawnTerl","GLOBAL")
	!Global("Chapter","GLOBAL",%bg2_chapter_4%)
	!Global("Chapter","GLOBAL",%bg2_chapter_5%)
	//!Global("Chapter","GLOBAL",%bg2_chapter_7%)
	See("Terl")  // Terl
	AreaType(OUTDOOR) //This might not be necessary
	Global("RomancePartFour","GLOBAL",0)
	!See([ENEMY])
	Global("_bAnomenClaraRomance","GLOBAL",2)
THEN
	RESPONSE #100
	Interact(Player1)
END


//Triggers Cor Talking if Clara dumped Anomen
IF
	Global("_bClaraAndCor","GLOBAL",1)
	Global("_bClaraAnomenLove","GLOBAL",24)
	InParty("Anomen") 
	See("Cor")
	!StateCheck("Anomen",CD_STATE_NOTVALID)
	!StateCheck("Cor",STATE_SLEEPING)  // Cor Delryn
	!StateCheck("Cor",STATE_STUNNED)  // Cor Delryn
	!StateCheck("Cor",STATE_HELPLESS)  // Cor Delryn
	!StateCheck("Cor",STATE_PANIC)  // Cor Delryn
	Global("_bClaraAndCorFix","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bClaraAndCorFix","GLOBAL",1)
END


IF
	Global("_bClaraAndCorFix","GLOBAL",1)
	Global("_bClaraAndCor","GLOBAL",1)
	Global("_bClaraAnomenLove","GLOBAL",24)
	InParty("Anomen") 
	!StateCheck("Anomen",CD_STATE_NOTVALID)
	!StateCheck("Cor",STATE_SLEEPING)  // Cor Delryn
	!StateCheck("Cor",STATE_STUNNED)  // Cor Delryn
	!StateCheck("Cor",STATE_HELPLESS)  // Cor Delryn
	!StateCheck("Cor",STATE_PANIC)  // Cor Delryn
THEN
	RESPONSE #100
	ActionOverride("Cor",StartDialogNoSet(Player1))
END

//This should make Anomen talk to you if timer expired first
IF
	Global("AnomenCor","GLOBAL",4)
	Global("_bLordAndLadyDelryn","GLOBAL",1)
	Global("_bACGonnaStabCor","GLOBAL",3)
	Global("_bCNMissedStabbing","GLOBAL",0)
	Detect(Player1)
THEN
	RESPONSE #100
	ActionOverride("Anomen",StartDialog("ANOMENP",Player1))
END

//This should make Anomen talk to you if he's returned to his house
IF
	Global("_bLordAndLadyDelryn","GLOBAL",4)
	Global("_bCorDieDamnIt","GLOBAL",1)
	Global("_bCrocodileTears","GLOBAL",0)
	Detect(Player1)
THEN
	RESPONSE #100
	ActionOverride("Anomen",StartDialog("ANOMENP",Player1))
END


//To change Anomen's sounds when he fails his order test.
IF
	Global("AnomenIsNotKnight","GLOBAL",1)
	Global("_bDarksideAnomenSounds","GLOBAL",0)
THEN
	RESPONSE #100
SetPlayerSound(Myself,70837,UNHAPPY_SERIOUS)  // *sigh* Mayhap it is time I thought of leaving. I can almost endure no more of your actions!
SetPlayerSound(Myself,70838,LEADER)  // Are you sure? I had enough of leadership in the Order. Oh, very well. *sigh* But it's a week vacation for everyone. *chuckle*
SetPlayerSound(Myself,70839,TIRED)  // A bottle of wine, a blanket, a little music as we rest... are these things too much to ask?
SetPlayerSound(Myself,70840,BORED)  // *chuckle* Hurry up and wait, hurry up and wait. Hm. Sounds like my days in the Order.
SetPlayerSound(Myself,70841,BATTLE_CRY1)  // Helm's fury upon you all!
SetPlayerSound(Myself,70842,BATTLE_CRY2)  // By skill and might, we shall prevail!
SetPlayerSound(Myself,70843,AREA_CITY)  // Hmm. I don't think I have ever looked upon the bustle of the city without keeping a watchful eye out for evil. It seems... quieter.
SetPlayerSound(Myself,70844,SELECT_COMMON1)  // I am ready to act.
SetPlayerSound(Myself,70845,SELECT_COMMON4)  // Yes?
SetPlayerSound(Myself,70846,SELECT_ACTION1)  // Of course.
SetPlayerSound(Myself,70847,SELECT_ACTION4)  // Not a problem.
SetPlayerSound(Myself,70848,SELECT_ACTION5)  // *chuckle* So long as I never have to breathe the same stuffy air as those rusted suits of armor in the Order... well, I'm with you.
SetPlayerSound(Myself,70840,SELECT_RARE2)  // *chuckle* Hurry up and wait, hurry up and wait. Hm. Sounds like my days in the Order.
SetGlobal("_bDarksideAnomenSounds","GLOBAL",1)
END

//To Turn off Anomen Attacking Dorn if he fails test
IF
	Global("AnomenIsNotKnight","GLOBAL",1)
	Global("_bDornAttacksTemple","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("OHD_anomenkeldorninterject","GLOBAL",9)
	SetGlobal("_bDornAttacksTemple","GLOBAL",1)
END

//Code for post Cor Anomen-Clara talks
IF
	IfValidForPartyDialog("Anomen")
	IfValidForPartyDialog("_bClara")
	Global("_bLordAndLadyDelryn","GLOBAL",5)
	!Global("AnomenRomanceActive","GLOBAL",1)
	!Global("AnomenRomanceActive","GLOBAL",2)
	OR(2)
	Global("_bClaraAnomenLove","GLOBAL",46)
	Global("_bClaraAnomenLove","GLOBAL",49)
THEN 
	RESPONSE #100
	SetGlobalTimer("_bCAMarriageTime","GLOBAL",TWO_DAYS)
	IncrementGlobal("_bClaraAnomenLove","GLOBAL",1)
END

IF	
	IfValidForPartyDialog("Anomen")
	IfValidForPartyDialog("_bClara")
	!Global("AnomenRomanceActive","GLOBAL",2)
	!Global("AnomenRomanceActive","GLOBAL",1)
	GlobalTimerExpired("_bCAMarriageTime","GLOBAL")
	OR(2)
	Global("_bClaraAnomenLove","GLOBAL",47)
	Global("_bClaraAnomenLove","GLOBAL",50)
	CombatCounter(0)
THEN 
	RESPONSE #100
	IncrementGlobal("_bClaraAnomenLove","GLOBAL",1)
END

IF
	IfValidForPartyDialog("Anomen")
	IfValidForPartyDialog("_bClara")
	!Global("AnomenRomanceActive","GLOBAL",2)
	!Global("AnomenRomanceActive","GLOBAL",1)
	OR(2)
	Global("_bClaraAnomenLove","GLOBAL",48)
	Global("_bClaraAnomenLove","GLOBAL",51)
	CombatCounter(0)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySong(35)
	StartDialogNoSet(Player1)
END

//Clearing Name Timer
IF
	InParty("Anomen")
	Global("_bAnomenClearsName","GLOBAL",2)
	Global("_bCorneilPaid","GLOBAL",1)
THEN
	RESPONSE #100
	SetGlobalTimer("_bCorneilTimer","GLOBAL",TWO_DAYS)
	SetGlobal("_bAnomenClearsName","GLOBAL",3)
END

IF
	InParty("Anomen")
	Global("_bAnomenClearsName","GLOBAL",3)
	GlobalTimerExpired("_bCorneilTimer","GLOBAL")
THEN
	RESPONSE #100
	SetGlobal("_bAnomenClearsName","GLOBAL",4)
END

IF
	InParty("Anomen")
	!StateCheck("Anomen",CD_STATE_NOTVALID)
	Global("_bAnomenClearsName","GLOBAL",4)
	See("corneil")
THEN
	RESPONSE #100
	ActionOverride("corneil",StartDialogNoSet(Player1))
END

//Terl Jysstev Letter Timer - Same Timer for Clara & CN
IF
	InParty("Anomen")
	Dead("aran")
	Global("_bCouncilSixTalk","GLOBAL",1) //set w/ CN & Clara
THEN
	RESPONSE #100
	SetGlobalTimer("_bTerl2Timer","GLOBAL",THREE_DAYS)
	SetGlobal("_bCouncilSixTalk","GLOBAL",2)
END

//Script to make Terl show up again
IF
	InParty("Anomen")
	!StateCheck("Anomen",CD_STATE_NOTVALID)
	GlobalTimerExpired("_bTerl2Timer","GLOBAL")
	!Global("Chapter","GLOBAL",%bg2_chapter_4%) //Chapter 4 (this is the code the buffer evaluates)
	!Global("Chapter","GLOBAL",%bg2_chapter_5%)  //Chapter 5 (Underdark)
	//!Global("Chapter","GLOBAL",%bg2_chapter_7%)  //Chapter 7  (Suldanesselar)
	//!AreaCheck("AR1900")  //Druid Grove Area
	//!AreaCheck("AR2500")   //Elven Temple at Underdark
	//!AreaCheck("AR2600")   //Thetyr Wood
	AreaType(OUTDOOR)
	!Exists("Terl")  // Terl
	CombatCounter(0)
	!AreaCheck("AR0041")   //Random Encounter
	!AreaCheck("AR0042")   //Random Encounter
	!AreaCheck("AR0043")   //Random Encounter
	!AreaCheck("AR0044")   //Random Encounter
	!AreaCheck("AR0045")   //Random Encounter
	!AreaCheck("AR0046")   //Random Encounter
	!AreaCheck("AR2601")   //Meet Drizzt
	!AreaCheck("AR2700")   //Ancient Spirits (mod only)
	!AreaCheck("AR2800")   //Suldanessellar
	!AreaCheck("AR2806")   //Tree of Life
	!AreaCheck("AR2807")   //Black Dragon Hideout	
	!AreaCheck("OH6010")   //Neera random encounter
	//!AreaCheck("OH6000")   //Wild Forest
	//!AreaCheck("OH6100")   //Hidden Refuge
	//!AreaCheck("OH6200")    //Hidden Refuge Destroyed
	!AreaCheck("OH7310")   //Hexxat - L Scene
	//!AreaCheck("OH5100")    //Resurrection Gorge
	//!AreaCheck("OH5200")    //Resurrection Gorge Interior
	//!AreaCheck("OH5300")    //Helmite Camp
	//!AreaCheck("OH4000")    //Rasaad Amphitheater
	//!AreaCheck("OH4100")    //Rasaad Dark Moon Heretic Temple
	Global("_bCouncilSixTalk","GLOBAL",2)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		CreateCreatureObjectEffect("bdoffscr","",Myself)  // No such index
		ApplySpellRES("bdoffscr","bdoffscr")  // No such index
		SmallWait(1)
		CreateCreatureObjectEffect("TERL","","bdoffscr")  // Terl
		SetInterrupt(TRUE)
END

IF
	InParty(Myself)
	!StateCheck("Anomen",CD_STATE_NOTVALID)
	GlobalTimerExpired("_bTerl2Timer","GLOBAL")
	!Global("Chapter","GLOBAL",%bg2_chapter_4%)
	!Global("Chapter","GLOBAL",%bg2_chapter_5%)
	//!Global("Chapter","GLOBAL",%bg2_chapter_7%)
	See("Terl")  // Terl
	AreaType(OUTDOOR)
	!See([ENEMY])
	Global("_bCouncilSixTalk","GLOBAL",2)
THEN
	RESPONSE #100
	SetGlobal("_bTerlFix1","GLOBAL",1) 
	Continue()
END

IF
	Global("_bTerlFix1","GLOBAL",1)
	Global("_bCouncilSixTalk","GLOBAL",2)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END


//Code to make Lord Jysstev appear if some mod destroys him
IF
	AreaCheck("AR1006")
	InParty(Myself)
	!Exists("scqar")
	Global("_bCouncilSixTalk","GLOBAL",3)
THEN
	RESPONSE #100
	CreateCreature("scqar",[904.328],S)
END
	
	
//Code for post Cor love talks w/ CN
IF
	InParty("Anomen")
	Global("_bDarksideAnomen","GLOBAL",2)
	Global("AnomenRomanceActive","GLOBAL",2)
	OR(2)
	Global("_bCNAnomenLove","GLOBAL",0)
	Global("_bCNAnomenLove","GLOBAL",3)
THEN 
	RESPONSE #100
	SetGlobalTimer("_bCNAnoTimer","GLOBAL",TWO_DAYS)
	IncrementGlobal("_bCNAnomenLove","GLOBAL",1)
END

IF	
	IfValidForPartyDialog("Anomen")
	GlobalTimerExpired("_bCNAnoTimer","GLOBAL")
	OR(2)
	Global("_bCNAnomenLove","GLOBAL",1)
	Global("_bCNAnomenLove","GLOBAL",4)
	CombatCounter(0)
THEN 
	RESPONSE #100
	IncrementGlobal("_bCNAnomenLove","GLOBAL",1)
END

IF
	IfValidForPartyDialog("Anomen")
	OR(2)
	Global("_bCNAnomenLove","GLOBAL",2)
	Global("_bCNAnomenLove","GLOBAL",5)
	CombatCounter(0)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySong(35)
	StartDialogNoSet(Player1)
END

//CN-Anomen Talk after name cleared
IF
	InParty("Anomen")
	Global("_bAnomenClearsName","GLOBAL",5)
	Global("_bCNAnomenLove","GLOBAL",6)
THEN 
	RESPONSE #100
	SetGlobalTimer("_bCACouncilTalk","GLOBAL",EIGHT_HOURS)
	IncrementGlobal("_bCNAnomenLove","GLOBAL",1)
END

IF	
	IfValidForPartyDialog("Anomen")
	GlobalTimerExpired("_bCACouncilTalk","GLOBAL")
	Global("_bCNAnomenLove","GLOBAL",7)
	Global("AnomenRomanceActive","GLOBAL",2)
	CombatCounter(0)
THEN 
	RESPONSE #100
	IncrementGlobal("_bCNAnomenLove","GLOBAL",1)
END

IF
	IfValidForPartyDialog("Anomen")
	Global("_bCNAnomenLove","GLOBAL",8)
	Global("AnomenRomanceActive","GLOBAL",2)
	CombatCounter(0)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySong(35)
	StartDialogNoSet(Player1)
END

//CN-Anomen Talk after talk with Jysstev
IF
	InParty("Anomen")
	Global("_bCouncilSixTalk","GLOBAL",4)
	Global("_bCNAnomenLove","GLOBAL",9)
THEN 
	RESPONSE #100
	SetGlobalTimer("_bCASeatTalk","GLOBAL",TWO_HOURS)
	IncrementGlobal("_bCNAnomenLove","GLOBAL",1)
END

IF	
	IfValidForPartyDialog("Anomen")
	GlobalTimerExpired("_bCASeatTalk","GLOBAL")
	Global("AnomenRomanceActive","GLOBAL",2)
	Global("_bCNAnomenLove","GLOBAL",10)
	CombatCounter(0)
THEN 
	RESPONSE #100
	IncrementGlobal("_bCNAnomenLove","GLOBAL",1)
END

IF
	IfValidForPartyDialog("Anomen")
	Global("_bCNAnomenLove","GLOBAL",11)
	Global("AnomenRomanceActive","GLOBAL",2)
	CombatCounter(0)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySong(35)
	StartDialogNoSet(Player1)
END

//Windspear Hills Quest
IF
	Global("MostNobleOrder","GLOBAL",0)
	Global("_bAnomenFearsOrder","GLOBAL",0)
	IfValidForPartyDialog("Anomen")
	OR(2)
		Global("FollowGarren","GLOBAL",1)
		Global("FollowedGarren","GLOBAL",1)
	OR(8)
		AreaCheck("AR0020") // City Gates
		AreaCheck("AR0300") //  The docks
		AreaCheck("AR0400") // The slums
		AreaCheck("AR0500 ") // Bridge district
		AreaCheck("AR0700") // Waukeen's Promenade
		AreaCheck("AR0800") // Graveyard
		AreaCheck("AR0900") // Temple District
		AreaCheck("AR1000") // Government District
THEN
	RESPONSE #100
	SetGlobal("_bAnomenFearsOrder","GLOBAL",1)
END

IF
	Global("_bAnomenFearsOrder","GLOBAL",1)
THEN RESPONSE #100
	StartDialogNoSet(Player1)
END

