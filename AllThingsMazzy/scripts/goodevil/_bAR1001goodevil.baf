
//Clara-Anomen Code to Kill Cor once timer expired
IF
	GlobalTimerExpired("_bAnomenClaraCorTimer","GLOBAL")
	Global("_bLordAndLadyDelryn","GLOBAL",0)
	Global("_bACGonnaStabCor","GLOBAL",2)
THEN
	RESPONSE #100
	SetGlobal("_bACGonnaStabCor","GLOBAL",3)
	ActionOverride("Cor",DestroySelf())
	Continue()
END

//This is to stop Anomen's normal dialogue from playing if he kills Cor with Clara
IF
	Global("AnomenCor","GLOBAL",2)
	Global("AnomenFinalFight","GLOBAL",1)
	Global("_bACGonnaStabCor","GLOBAL",1)
	Global("_bCorFight","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bCorFight","GLOBAL",1)
	SetGlobal("AnomenCor","GLOBAL",5)
	Continue()
END


//This should play Cor's normal dialogue if Anomen dies - adjusted code from game
IF
	Global("AnomenCor","GLOBAL",5)
	Global("_bACGonnaStabCor","GLOBAL",1)
	Global("_bCorExtra","GLOBAL",0)
	Dead("Anomen")  // Anomen
	!Dead("Cor")  // Cor Delryn
	!StateCheck("Cor",STATE_SLEEPING)  // Cor Delryn
	!StateCheck("Cor",STATE_STUNNED)  // Cor Delryn
	!StateCheck("Cor",STATE_HELPLESS)  // Cor Delryn
	!StateCheck("Cor",STATE_PANIC)  // Cor Delryn
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("AnomenCor","GLOBAL",3)
	SetGlobal("_bCorExtra","GLOBAL",1)
	ActionOverride("Cor",StartDialog("COR",Player1))
	Continue()
END

//This triggers new dialogue if Anomen kills Cor w/ Clara
IF
	Global("AnomenCor","GLOBAL",5)
	Global("_bCorFight","GLOBAL",1)
	Global("_bACGonnaStabCor","GLOBAL",1)
	Dead("Cor")  // Cor Delryn
	!Dead("Anomen")  // Anomen
	!StateCheck("Anomen",CD_STATE_NOTVALID)
	CombatCounter(0)
THEN
	RESPONSE #100
	ActionOverride("Anomen",StartDialog("ANOMENP",Player1))
	Continue()
END

//Triggers Fade if Clara marries Cor
IF
	Global("_bLordAndLadyDelryn","GLOBAL",3)
	Global("_bClaraFilesPapers","GLOBAL",1)
THEN
	RESPONSE #100
	ActionOverride("_bClara",EscapeAreaMove("AR1001",511,283,NE))
	SetGlobal("_bClaraFilesPapers","GLOBAL",2)
END

IF 
	Global("_bClaraFilesPapers","GLOBAL",2)
THEN
	RESPONSE #100
	FadeToColor([20.0],0) 
	Wait(3)
	SetGlobal("_bClaraFilesPapers","GLOBAL",3)
END

IF 
	Global("_bClaraFilesPapers","GLOBAL",3)
THEN
	RESPONSE #100
	ActionOverride("_bClara",StartDialog("_BCLARAP",Player1))
END


//Timer before they'll travel again
IF
	GlobalTimerExpired("_bAnomenClaraCorTimer2","GLOBAL")
	Global("_bLordAndLadyDelryn","GLOBAL",1)
THEN
	RESPONSE #100
	SetGlobal("_bLordAndLadyDelryn","GLOBAL",2)
END

//To make sure Cor dies
IF
	GlobalTimerExpired("_bAnomensReturn","GLOBAL")
	Global("_bCorDieDamnIt","GLOBAL",0)
THEN
	RESPONSE #100
	FadeToColor([20.0],0) //So he dies and Anomen appears a little less abruptly
	Wait(2)
	ActionOverride("Cor",DestroySelf())
	FadeFromColor([20.0],0)
	SetGlobal("_bCorDieDamnIt","GLOBAL",1)
	Continue()
END

//Wedding Timer - post widowing
IF
	Global("_bLordAndLadyDelryn","GLOBAL",4)
	Global("_bCrocodileTears","GLOBAL",1)
	GlobalTimerExpired("_bAnomenClaraTimer3","GLOBAL")
THEN
	RESPONSE #100
	SetGlobal("_bLordAndLadyDelryn","GLOBAL",2)
END

//Timer for Travel after Cor Wedding
IF
	Global("_bClaraAndCor","GLOBAL",2)
	Global("_bLadyDelryn","GLOBAL",1)
	GlobalTimerExpired("_bCorWeddingTravel","GLOBAL")
THEN
	RESPONSE #100
	SetGlobal("_bClaraAndCor","GLOBAL",3)
END

//To remove Cor's body after the rest in Darkside Anomen
IF
	OR(2)
	Global("_bLordAndLadyDelryn","GLOBAL",2)
	Global("_bDarksideAnomen","GLOBAL",2)
	Global("_bDestroyBodyofCor","GLOBAL",0)
THEN
	RESPONSE #100
	ActionOverride("Cor",DestroySelf())
	SetGlobal("_bDestroyBodyofCor","GLOBAL",1)
	Continue()
END






