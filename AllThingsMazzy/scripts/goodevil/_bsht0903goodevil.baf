
//This script turns all the Order hostile if you go to the temple district.
IF
	OR(2)
		Global("FollowGarren","GLOBAL",1)
		Global("FollowedGarren","GLOBAL",1)
	Global("TempleShout0903","GLOBAL",0)
	Global("_bPrelateSurrenders","GLOBAL",0)
	See([PC])
THEN
	RESPONSE #100
	SetGlobal("TempleShout0903","GLOBAL",1)
END

//This script is for if Bodhi's in your party.
IF 
	See("_bBodhi")
	Global("TempleShout0903","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("TempleShout0903","GLOBAL",1)
END

//This script is to turn them back neutral. It'll work as long as you don't attack them again.
IF
	Global("TempleShout0903","GLOBAL",0)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
	ClearAllActions()
	ChangeEnemyAlly(Myself,NEUTRAL)
	ClearAllActions()
END

//The script to turn this shout off if Garren Windspear intervenes for you is in baldur.bcs

//This script starts the dialogue with HPrelate if he's hostile.
IF
	Name("hprelate",Myself)
	Allegiance(Myself,ENEMY)
	Global("_bPrelateYourEnemy","GLOBAL",0)
	See([PC])
THEN
	RESPONSE #100
	SetGlobal("_bPrelateYourEnemy","GLOBAL",1)
END

IF
	Global("_bPrelateYourEnemy","GLOBAL",1)
	Name("hprelate",Myself)
	See([PC])
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Dialogue if HPrelate if he's almost dead.
IF
	Name("hprelate",Myself)
	Allegiance(Myself,ENEMY)
	HPLT(Myself,10)
	Global("_bPrelateSurrenders","GLOBAL",0)
	!IfValidForPartyDialog("_bBodhi")
THEN
	RESPONSE #100
	ClearAllActions()
	SetGlobal("_bPrelateSurrenders","GLOBAL",1)
	StartDialogNoSet(Player1)
END


