
//ClaraSex scene trigger
IF
	Global("_bSleepsWithClara","GLOBAL",4) 
	InParty("DORN") 
	!StateCheck("DORN",CD_STATE_NOTVALID) 
	Global("OHD_dornsex","GLOBAL",1) 
	Global("_bSWCD","GLOBAL",0)
	!Global("DornRomanceActive","GLOBAL",3)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Final Clara Banter Trigger - 1st in d
IF 
	InParty("_bClara") 
	See("_bClara") 
	!StateCheck("DORN",CD_STATE_NOTVALID) 
	!StateCheck("_bClara",CD_STATE_NOTVALID) 
	Global("_BBCLARADORN","GLOBAL",3)
	!Global("DornRomanceActive","GLOBAL",2)
	AreaType(FOREST)
THEN
	RESPONSE #100
	UndoExplore()
	Interact("_bClara")
END

//Aerie Wing Reponse
IF
	Global("_bAerieWingResponse","GLOBAL",1)
	InParty("DORN")
	!StateCheck("DORN",CD_STATE_NOTVALID)
	Global("_bDornWingResponse","GLOBAL",0)
	CombatCounter(0)
THEN 
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Dorn Umberlee Spell
IF
	Global("_bDornHasUmberleeSpell","GLOBAL",1)
	Global("_bDornHasUmberleeSpellScript","GLOBAL",0)
THEN
	RESPONSE #100
	AddSpecialAbility("_BDORNUM")
	SetGlobal("_bDornHasUmberleeSpellScript","GLOBAL",1)
END

//Dorn Temple talk if fallen.
IF
IfValidForPartyDialog("Dorn")
OR(2)
AreaCheck("AR1600")  //Brynnlaw
AreaCheck("AR0900")  //Temple
FallenPaladin("Dorn")
Global("_bDornPaladinTalos","GLOBAL",0)
THEN
	RESPONSE #100
SetGlobal("_bDornPaladinTalos","GLOBAL",1)
END

IF
Global("_bDornPaladinTalos","GLOBAL",1)
THEN
	RESPONSE #100
StartDialogNoSet(Player1)
END

//Dorn Talos Spells
IF
	Global("_bDornHasTalosSpells","GLOBAL",1)
	Global("_bDornHasTalosSpellScript","GLOBAL",0)
THEN
	RESPONSE #100
	AddSpecialAbility("SPPR302") //Cleric_Call_Lightning
	AddSpecialAbility("SPCL721") //TALOS_STORMSHIELD
	AddSpecialAbility("SPDR301") //Druid Lightning Bolt
	SetGlobal("_bDornHasTalosSpellScript","GLOBAL",1)
END


//Beginning of HaerDalis-Dorn romance
IF
	!Global("DornRomanceActive","GLOBAL",1) 
	!Global("DornRomanceActive","GLOBAL",2)
	!Global("_bHDDORNROMANCE","GLOBAL",3) //Romance not turned off
	!Global("LS_HaerDalisRomanceActive","GLOBAL",2)
	!Global("LS_HaerDalisRomanceActive","GLOBAL",1)
	GlobalGT("OHD_haerdalis","GLOBAL",0)   //Starts after first banter triggers
	IfValidForPartyDialog("Dorn")
	IfValidForPartyDialog("HaerDalis")
	OR(3)
	Global("_bDornHDLove","GLOBAL",0)
	Global("_bDornHDLove","GLOBAL",7)
	Global("_bDornHDLove","GLOBAL",10)
THEN 
	RESPONSE #100
	SetGlobalTimer("_bDHDLoveTimer","GLOBAL",THREE_DAYS)
	IncrementGlobal("_bDornHDLove","GLOBAL",1)
END

IF //All the requirements need to be in this one so that the talk isn't true before it plays. Otherwise, it will sometimes block other talks.
	!Global("DornRomanceActive","GLOBAL",1) 
	!Global("DornRomanceActive","GLOBAL",2)
	!Global("LS_HaerDalisRomanceActive","GLOBAL",2)
	!Global("LS_HaerDalisRomanceActive","GLOBAL",1)
	!Global("_bHDDORNROMANCE","GLOBAL",3) //Romance not turned off
	IfValidForPartyDialog("Dorn")
	IfValidForPartyDialog("HaerDalis")
	GlobalTimerExpired("_bDHDLoveTimer","GLOBAL")
	OR(4)
	Global("_bDornHDLove","GLOBAL",1) //4 & 19 are in the d script, no timer
	Global("_bDornHDLove","GLOBAL",8) 
	Global("_bDornHDLove","GLOBAL",11)
	Global("_bDornHDLove","GLOBAL",17) //Always pre or post Aerie conflict (1st trigger below)
	CombatCounter(0)
THEN 
	RESPONSE #100
	IncrementGlobal("_bDornHDLove","GLOBAL",1)
END

IF
	OR(6)
	Global("_bDornHDLove","GLOBAL",2)
	Global("_bDornHDLove","GLOBAL",4) //This talk triggered in d script
	Global("_bDornHDLove","GLOBAL",9)
	Global("_bDornHDLove","GLOBAL",12)
	Global("_bDornHDLove","GLOBAL",18) //Always pre or post Aerie conflict
	Global("_bDornHDLove","GLOBAL",20)
THEN
	RESPONSE #100
	PlaySong(0)   //This should turn off any other game music  (this does not affect sounds)
	PlaySong(87)  //Dorn Romance music
	StartDialogNoSet(Player1)
END

//Talk 6 first trigger, seperate b/c of Aerie. 
IF
	!Global("DornRomanceActive","GLOBAL",1) 
	!Global("DornRomanceActive","GLOBAL",2)
	!Global("_bHDDORNROMANCE","GLOBAL",3) //Romance not turned off
	!Global("LS_HaerDalisRomanceActive","GLOBAL",2)
	!Global("LS_HaerDalisRomanceActive","GLOBAL",1)
	IfValidForPartyDialog("Dorn")
	IfValidForPartyDialog("HaerDalis")
	OR(3)
		Global("_bAerieDornConflict","GLOBAL",0) //Aerie conflict must not have started (irrelevant for all talks pre #16
		Global("_bAerieDornFight","GLOBAL",3)  //Or conflict must be finished
		!InParty("Aerie") //Or Aerie not in party
	Global("_bDornHDLove","GLOBAL",16) 
THEN 
	RESPONSE #100
	SetGlobalTimer("_bDHDLoveTimer","GLOBAL",THREE_DAYS)
	IncrementGlobal("_bDornHDLove","GLOBAL",1)
END


//Sparring Fade
IF
	Global("_bDornHDLove","GLOBAL",5)
THEN
	RESPONSE #100
	ClearAllActions()
	PlaySong(0) 
	PlaySong(87) 
	ActionOverride("HaerDalis",MoveToObject("Dorn"))
	Wait(1)
	FadeToColor([20.0],0)
	Wait(2)
	SetGlobal("_bDornHDLove","GLOBAL",67)
END

IF
	Global("_bDornHDLove","GLOBAL",67)
THEN
	RESPONSE #100	
	ApplySpellRES("_BTIRED","Dorn")
	ApplySpellRES("_BTIRED","HaerDalis")
	Wait(2)
	FadeFromColor([20.0],0)
	Wait(2)
	SetGlobal("_bDornHDLove","GLOBAL",6)
END

IF
	Global("_bDornHDLove","GLOBAL",6)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Post Rest Talk Triggers - Talk 7.5 
IF 
	!Global("DornRomanceActive","GLOBAL",1) 
	!Global("DornRomanceActive","GLOBAL",2)
	!Global("LS_HaerDalisRomanceActive","GLOBAL",2)
	!Global("LS_HaerDalisRomanceActive","GLOBAL",1)
	IfValidForPartyDialog("Dorn")
	IfValidForPartyDialog("HaerDalis")
	Global("_bDornHDLove","GLOBAL",21) //Next trigger for this in HD
	CombatCounter(0)
THEN 
	RESPONSE #100
	ApplySpellRES("_BTIRED","Dorn")
	ApplySpellRES("_BTIRED","HaerDalis")
	IncrementGlobal("_bDornHDLove","GLOBAL",1)
END


//5th Talk Trigger (Requires 4th but separate trigger b/c of areas) (This does not require Aerie conflict first)
IF 
	!Global("DornRomanceActive","GLOBAL",1) 
	!Global("DornRomanceActive","GLOBAL",2)
	!Global("LS_HaerDalisRomanceActive","GLOBAL",2)
	!Global("LS_HaerDalisRomanceActive","GLOBAL",1)
	!Global("_bHDDORNROMANCE","GLOBAL",3) //Romance not turned off
	IfValidForPartyDialog("Dorn")
	IfValidForPartyDialog("HaerDalis")
	Global("_bDornHDLove","GLOBAL",13)
THEN 
	RESPONSE #100
	SetGlobalTimer("_bDHDLoveTimer","GLOBAL",FOUR_DAYS)
	IncrementGlobal("_bDornHDLove","GLOBAL",1)
END	
		
IF 
	!Global("DornRomanceActive","GLOBAL",1) 
	!Global("DornRomanceActive","GLOBAL",2)
	!Global("_bHDDORNROMANCE","GLOBAL",3) //Romance not turned off
	!Global("LS_HaerDalisRomanceActive","GLOBAL",2)
	!Global("LS_HaerDalisRomanceActive","GLOBAL",1)
	IfValidForPartyDialog("Dorn")
	IfValidForPartyDialog("HaerDalis")
	GlobalTimerExpired("_bDHDLoveTimer","GLOBAL")
	Global("_bDornHDLove","GLOBAL",14)
	OR(18)
		AreaCheck("AR0021")       // City Gates - Crooked Crane 1st floor
		AreaCheck("AR0022")       // City Gates - Crooked Crane 2nd floor
		AreaCheck("AR0313")       // Docks - Sea's Bounty 1st floor
	    AreaCheck("AR0314")       // Docks - Sea's Bounty 2nd floor	
		AreaCheck("AR0406")       // Slums - Copper Coronet
		AreaCheck("AR0509")       // Bridge - Five Flagons 1st floor
		AreaCheck("AR0511")       // Bridge - Five Flagons 2nd Floor
		AreaCheck("AR0513")       // Bridge - Calbor's Inn 1st floor
		AreaCheck("AR0514")       // Bridge - Inn 2nd floor
		AreaCheck("AR0515")       // Bridge - Inn 3rd floor
		AreaCheck("AR0522")       // Bridge - Five Flagons 1st floor (stronghold)
		AreaCheck("AR0704")       // Waukeen's Promenade - Mithrest Inn
		AreaCheck("AR0709")       // Waukeen's Promenade - Den of the Seven Vales, 1st Floor
		AreaCheck("AR0712")       // Waukeen's Promenade - Den of the Seven Vales, 2nd Floor
		AreaCheck("AR1105")       // Umar Hills - Imnesvale Inn
		AreaCheck("AR1602")       // Brynnlaw - Brynnlaw Inn
		AreaCheck("AR2010")       // Trademeet - Vytori's Pub
		AreaType(CITY)
		!AreaCheck("AR2200")    //Not Ust Natha
	CombatCounter(0)
THEN 
	RESPONSE #100
	IncrementGlobal("_bDornHDLove","GLOBAL",1)
END

IF
	Global("_bDornHDLove","GLOBAL",15)
THEN
	RESPONSE #100
	PlaySong(0)   //This should turn off any other game music  (this does not affect sounds)
	PlaySong(87)  //Dorn Romance music
	StartDialogNoSet(Player1)
END

//Aerie Conflict Trigger (1st in HD)
IF 
	!Global("DornRomanceActive","GLOBAL",1) 
	!Global("DornRomanceActive","GLOBAL",2)
	!Global("LS_HaerDalisRomanceActive","GLOBAL",2)
	!Global("LS_HaerDalisRomanceActive","GLOBAL",1)
	!Global("_bHDDORNROMANCE","GLOBAL",3) //Romance not turned off
	IfValidForPartyDialog("Dorn")
	IfValidForPartyDialog("HaerDalis")
	IfValidForPartyDialog("Aerie")
	See("Aerie")
	Range("Aerie",15)
	GlobalTimerExpired("_bDHDAerieTimer","GLOBAL")
	Global("_bAerieDornConflict","GLOBAL",1)
	!AreaType(DUNGEON)  //Not in a dungeon, that would be mean.
	CombatCounter(0)
THEN 
	RESPONSE #100
	IncrementGlobal("_bAerieDornConflict","GLOBAL",1)
END

IF
	Global("_bAerieDornConflict","GLOBAL",2)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END


//Dorn-Aerie Conflict Fight Script
IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	InParty("Dorn")
	Global("_bAerieDornFight","GLOBAL",1)
THEN
	RESPONSE #100
	ChangeAIScript("",DEFAULT)
	SetLeavePartyDialogFile() 
	LeaveParty()
	DialogInterrupt(FALSE)
	Continue()
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Global("_bAerieDornFight","GLOBAL",1)
	OR(4)
	Range("Aerie",7)
	HasItemEquiped("SHLD24","Aerie")  // Reflection Shield +1
	HasItemEquiped("D1REFSHD","Aerie")  // Reflection Shield +3 (modded)
	HasItemEquiped("SHLD24A","Aerie")  // Reflection Shield +1 (small version)
THEN
	RESPONSE #100
	EquipMostDamagingMelee()
	Continue()
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Global("_bAerieDornFight","GLOBAL",1)
	!GlobalTimerNotExpired("CAST","LOCALS")
	!CheckSpellState(Myself,ARMOR_OF_FAITH)
	HaveSpell(CLERIC_ARMOR_OF_FAITH) 
	RandomNum(2,1)
THEN
	RESPONSE #100
	SetGlobalTimer("CAST","LOCALS",ONE_ROUND)
	Spell(Myself,CLERIC_ARMOR_OF_FAITH) 
	Continue()
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Global("_bAerieDornFight","GLOBAL",1)
	HaveSpell(ASSASSIN_POISON)
	!CheckSpellState(Myself,POISON_WEAPON) 
THEN
	RESPONSE #100
	Spell(Myself,ASSASSIN_POISON)  // SPCL423.SPL (Poison Weapon)
	AttackReevaluate("Aerie",30)
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Global("_bAerieDornFight","GLOBAL",1)
	HaveSpell(BLACKGUARD_ABSORB_HEALTH)  // SPCL102.SPL (Absorb Health)
	HPPercentLT(Myself,50)
THEN
	RESPONSE #100
	Spell("Aerie",BLACKGUARD_ABSORB_HEALTH)  // Aerie
	AttackReevaluate("Aerie",45)
	Continue()
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	!GlobalTimerNotExpired("CAST","LOCALS")
	Global("_bAerieDornFight","GLOBAL",1)
	HaveSpell(CLERIC_SILENCE_15_FOOT)  // SPPR211.SPL 
	!StateCheck("Aerie",STATE_SILENCED)
	RandomNum(2,1)
THEN
	RESPONSE #100
	SetGlobalTimer("CAST","LOCALS",ONE_ROUND)
	Spell("Aerie",CLERIC_SILENCE_15_FOOT)  // Aerie
	AttackReevaluate("Aerie",45)
	Continue()
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Global("_bAerieDornFight","GLOBAL",1)
	!See("Aerie")
THEN
	RESPONSE #100
	MoveToObject("Aerie")
	AttackReevaluate("Aerie",45)
	Continue()
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	!GlobalTimerNotExpired("CAST","LOCALS")
	Global("_bAerieDornFight","GLOBAL",1)
	HaveSpell(CLERIC_HOLD_PERSON)  // SPPR208.SPL (Hold Person)
	!StateCheck("Aerie",STATE_HELPLESS)
	RandomNum(2,1)
THEN
	RESPONSE #100
	SetGlobalTimer("CAST","LOCALS",ONE_ROUND)
	Spell("Aerie",CLERIC_HOLD_PERSON)  // Aerie
	AttackReevaluate("Aerie",45)
	Continue()
END

IF  //to reset him attacking after casting spells
	Global("_bAerieDornFight","GLOBAL",1)
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	OR(2)
		CheckSpellState(Myself,POISON_WEAPON) 
		!HaveSpell(ASSASSIN_POISON)
THEN
	RESPONSE #100
	AttackReevaluate("Aerie",45)
END


//To start talk if Dorn wins
IF
	Dead("Aerie")  // Aerie
	CombatCounter(0)
	Global("_bAerieDornFight","GLOBAL",1)
THEN
	RESPONSE #100
	JoinParty()
	SetGlobal("_bAerieDornFight","GLOBAL",2)
END

IF
	Global("_bAerieDornFight","GLOBAL",2)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Talks Once Romance Active
//When HD badly injured
IF 
	Global("_bHDDORNROMANCE","GLOBAL",2) //romance active. You shouldn't be able to romance Dorn or HD at this point so leaving off those checks
	HPPercentLT("HaerDalis",30)  //Changed from fatigue b/c too hard to trigger w/o triggering post sex
	IfValidForPartyDialog("Dorn")
	IfValidForPartyDialog("HaerDalis")
	Global("_bDHDTiredTalk","GLOBAL",0)
	CombatCounter(0)
THEN 
	RESPONSE #100
	SetGlobal("_bDHDTiredTalk","GLOBAL",1)
END

IF
	Global("_bDHDTiredTalk","GLOBAL",1)
THEN
	RESPONSE #100
	PlaySong(0)  
	PlaySong(87)  //Dorn Romance music
	StartDialogNoSet(Player1)
END


//Timer for romance talks, post sex
IF
	Global("_bHDDORNROMANCE","GLOBAL",2) //romance active. You shouldn't be able to romance Dorn or HD at this point so leaving off those checks
	IfValidForPartyDialog("Dorn")
	IfValidForPartyDialog("HaerDalis")
	Global("_bDornHDLove","GLOBAL",23)
THEN 
	RESPONSE #100
	SetGlobalTimer("_bDHDLoveTimer2","GLOBAL",FOUR_DAYS)
	IncrementGlobal("_bDornHDLove","GLOBAL",1)
END

IF
	GlobalTimerExpired("_bDHDLoveTimer2","GLOBAL")
	Global("_bHDDORNROMANCE","GLOBAL",2) //romance active. 
	IfValidForPartyDialog("Dorn")
	IfValidForPartyDialog("HaerDalis")
	Global("_bDornHDLove","GLOBAL",24)
	CombatCounter(0)
THEN 
	RESPONSE #100
	IncrementGlobal("_bDornHDLove","GLOBAL",1)
END

IF
	Global("_bDornHDLove","GLOBAL",25) //Heritage Talk
THEN
	RESPONSE #100
	PlaySong(0)  
	PlaySong(87)  //Dorn Romance music
	StartDialogNoSet(Player1)
END


//Romance Combat Talk - HD Injured
IF
	Global("_bHDDORNROMANCE","GLOBAL",2)
	HPPercentLT("HaerDalis",50)
	IfValidForPartyDialog("Dorn")
	IfValidForPartyDialog("HaerDalis")
	Global("_bHDHurt","GLOBAL",0)
	OR(2)
	Global("_bDornHurt","GLOBAL",0)
	Global("_bDornHurt","GLOBAL",3)
	ActuallyInCombat()
THEN 
	RESPONSE #100
	SetGlobal("_bHDHurt","GLOBAL",1)
END

IF
	Global("_bHDDORNROMANCE","GLOBAL",2)
	Global("_bHDHurt","GLOBAL",1)
	HPPercentLT("HaerDalis",50)
	IfValidForPartyDialog("Dorn")
	IfValidForPartyDialog("HaerDalis")
	OR(2)
	Global("_bDornHurt","GLOBAL",0)
	Global("_bDornHurt","GLOBAL",3)
	ActuallyInCombat()
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

IF
	Global("_bHDHurt","GLOBAL",2)
	CombatCounter(0)
THEN 
	RESPONSE #100
	SetGlobal("_bHDHurt","GLOBAL",3)
END	

//Code to trigger talk pre Ritual talk faster since I bogarted the in-game trigger
IF
	Global("OHD_ritual","OH5120",4) 
	IfValidForPartyDialog("Dorn")
	Global("OHD_ritual_target","OH5120",5) 
	Global("_bHDDORNROMANCE","GLOBAL",2)
	Global("_bHDRitual","OH5120",1)
THEN 
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Code to put HD back in your party so he can be raised
IF
	Global("_bHDRitual","OH5120",1)
	GlobalGT("OHD_ritual","OH5120",4)
	Dead("HaerDalis")
THEN 
	RESPONSE #100
	ActionOverride("HaerDalis",JoinParty())
	SetGlobal("_bHDRitual","OH5120",2)
END

//If Dorn gives up power
IF
	Global("_bHDDORNROMANCE","GLOBAL",2) //romance active.
	IfValidForPartyDialog("Dorn")
	InPartyAllowDead("HaerDalis")
	FallenPaladin("Dorn") //No longer a blackguard
	Global("OHD_ritual","OH5120",20) //Both demons dead
	AreaCheck("OH5120")	 //Resurrection Gorge Tree Interior
	Global("_bDornHDPowerTalk","GLOBAL",0)
THEN 
	RESPONSE #100
	SetGlobalTimer("_bDHDLoveTimer3","GLOBAL",ONE_HOUR)
	IncrementGlobal("_bDornHDPowerTalk","GLOBAL",1)
END

IF
	GlobalTimerExpired("_bDHDLoveTimer3","GLOBAL")
	Global("_bHDDORNROMANCE","GLOBAL",2) //romance active. 
	IfValidForPartyDialog("Dorn")
	IfValidForPartyDialog("HaerDalis")
	Global("_bDornHDPowerTalk","GLOBAL",1)
	CombatCounter(0)
THEN 
	RESPONSE #100
	IncrementGlobal("_bDornHDPowerTalk","GLOBAL",1)
END

IF
	Global("_bDornHDPowerTalk","GLOBAL",2) 
THEN
	RESPONSE #100
	PlaySong(0)  
	PlaySong(87)  //Dorn Romance music
	StartDialogNoSet(Player1)
END

//If Dorn gains power (in ritual or temples)
IF
	Global("_bHDDORNROMANCE","GLOBAL",2) //romance active.
	IfValidForPartyDialog("Dorn")
	InPartyAllowDead("HaerDalis")
	OR(3) 
	Global("OHD_ritual","OH5120",10) //Ritual & fight completely over
	Global("_bDornHasUmberleeSpell","GLOBAL",1) //Got Umberlee spell powers
	Global("_bDornHasTalosSpells","GLOBAL",1)   //Got Talos spell powers
	!FallenPaladin("Dorn") //Not fallen
	OR(3)
	AreaCheck("OH5120")	 //Resurrection Gorge Tree Interior
	AreaCheck("AR0904")  //Temple of Talos 
	AreaCheck("AR1604")   //Temple of Umberlee
	Global("_bDornHDPowerTalk2","GLOBAL",0)
THEN 
	RESPONSE #100
	SetGlobalTimer("_bDHDLoveTimer4","GLOBAL",ONE_HOUR)
	IncrementGlobal("_bDornHDPowerTalk2","GLOBAL",1)
END

IF
	GlobalTimerExpired("_bDHDLoveTimer4","GLOBAL")
	Global("_bHDDORNROMANCE","GLOBAL",2) //romance active. 
	IfValidForPartyDialog("Dorn")
	IfValidForPartyDialog("HaerDalis")
	OR(3) 
	Global("OHD_ritual","OH5120",10) //Ritual & fight completely over
	Global("_bDornHasUmberleeSpell","GLOBAL",1) //Got Umberlee spell powers
	Global("_bDornHasTalosSpells","GLOBAL",1)   //Got Talos spell powers
	!FallenPaladin("Dorn") //Not fallen
	Global("_bDornHDPowerTalk2","GLOBAL",1)
	CombatCounter(0)
THEN 
	RESPONSE #100
	IncrementGlobal("_bDornHDPowerTalk2","GLOBAL",1)
END

IF
	Global("_bDornHDPowerTalk2","GLOBAL",2) 
THEN
	RESPONSE #100
	PlaySong(0)  
	PlaySong(87)  //Dorn Romance music
	StartDialogNoSet(Player1)
END


//Suldanessellar Talk
IF
	Global("_bHDDORNROMANCE","GLOBAL",2) //romance active.
	IfValidForPartyDialog("Dorn")
	IfValidForPartyDialog("HaerDalis")
	AreaCheck("AR2800")	 //Suldanessellar
	Global("_bDornC7Talk","GLOBAL",0)
THEN 
	RESPONSE #100
	SetGlobalTimer("_bDHDLoveTimer4","GLOBAL",ONE_HOUR)
	IncrementGlobal("_bDornC7Talk","GLOBAL",1)
END

IF
	GlobalTimerExpired("_bDHDLoveTimer4","GLOBAL")
	Global("_bHDDORNROMANCE","GLOBAL",2) //romance active. 
	IfValidForPartyDialog("Dorn")
	IfValidForPartyDialog("HaerDalis")
	AreaCheck("AR2800")	 //Suldanessellar
	Global("_bDornC7Talk","GLOBAL",1)
THEN 
	RESPONSE #100
	IncrementGlobal("_bDornC7Talk","GLOBAL",1)
END

IF
	Global("_bDornC7Talk","GLOBAL",2) 
THEN
	RESPONSE #100
	PlaySong(0)  
	PlaySong(87)  //Dorn Romance music
	StartDialogNoSet(Player1)
END


//Joining if HD is fighting CN
IF
	!InParty("HaerDalis")
	Allegiance("HaerDalis",ENEMY)
	Detect("HaerDalis")
	Global("_bDornJoinsHD","GLOBAL",0)
	Global("_bHDDORNROMANCE","GLOBAL",2)
	IfValidForPartyDialog("Dorn")
THEN
	RESPONSE #100
	SetGlobal("_bDornJoinsHD","GLOBAL",1)
END

IF
	Global("_bDornJoinsHD","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Start of Trigger for Helmite Camp Talk (next trigger in HD)
IF
	OR(2) Global("_bHDDORNROMANCE","GLOBAL",1) //starting
	Global("_bHDDORNROMANCE","GLOBAL",2) //romance active.
	IfValidForPartyDialog("Dorn")
	IfValidForPartyDialog("HaerDalis")
	GlobalGT("OHD_PostCamp","LOCALS",6)  //Had Dorn talk at Helmite camp
	!Detect("OHDHAMMS") //Don't see Helmite ambush
	!FallenPaladin("Dorn")  //Dorn has a patron - demon/Talos/Umberlee
	Global("_bHelmiteTalk","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bHelmiteTalk","GLOBAL",1)
	SetGlobalTimer("_bHelmiteTimer","GLOBAL",FIVE_MINUTES)
END

//Breakup Talk
IF
	Global("DornRomanceActive","GLOBAL",3) 
	Global("_bMazNotDorn","GLOBAL",1)
	InParty("Dorn") 
	InParty("Mazzy")
THEN
	RESPONSE #100
	SetGlobalTimer("_bDornBreakUp","GLOBAL",TEN_MINUTES)
	SetGlobal("_bMazNotDorn","GLOBAL",2)
END

IF
	GlobalTimerExpired("_bDornBreakUp","GLOBAL") 
	Global("_bMazNotDorn","GLOBAL",2)
	IfValidForPartyDialogue("Dorn")
	IfValidForPartyDialogue("Mazzy")
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bMazNotDorn","GLOBAL",3)
END

IF
	Global("_bMazNotDorn","GLOBAL",3)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySound("_bMaz1")
	StartDialogNoSet(Player1)
END



