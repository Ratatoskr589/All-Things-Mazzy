//Hexxat Talks
IF
	Global("OHH_viconia","GLOBAL",1)
	InParty("Viconia")
	InParty("HEXXAT")
	Global("_bHexVic","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bHexVic","GLOBAL",1)
	SetGlobalTimer("_bHexVicSexTimer","GLOBAL",ONE_DAY)
END

IF
	IfValidForPartyDialogue("Viconia")
	IfValidForPartyDialogue("HEXXAT")
	Global("_bHexVic","GLOBAL",1)
	GlobalTimerExpired("_bHexVicSexTimer","GLOBAL")
	CombatCounter(0)
	!Global("ViconiaRomanceActive","GLOBAL",2)
	!Global("ViconiaRomanceActive","GLOBAL",1)
	!Global("HexxatRomanceActive","GLOBAL",1)
	!Global("HexxatRomanceActive","GLOBAL",2)
THEN
	RESPONSE #100
	SetGlobal("_bHexVic","GLOBAL",2)
END

IF
	Global("_bHexVic","GLOBAL",2)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//2nd Talk timer (rest in Hexxat)
IF
	InParty("Viconia")
	InParty("HEXXAT")
	Global("_bHexVic","GLOBAL",3)
THEN
	RESPONSE #100
	SetGlobal("_bHexVic","GLOBAL",4)
	SetGlobalTimer("_bHexVicSexTimer2","GLOBAL",ONE_DAY)
END

IF
	IfValidForPartyDialogue("Viconia")
	IfValidForPartyDialogue("HEXXAT")
	Global("_bHexVic","GLOBAL",4)
	GlobalTimerExpired("_bHexVicSexTimer2","GLOBAL")
	CombatCounter(0)
	!Global("ViconiaRomanceActive","GLOBAL",2)
	!Global("ViconiaRomanceActive","GLOBAL",1)
	!Global("HexxatRomanceActive","GLOBAL",1)
	!Global("HexxatRomanceActive","GLOBAL",2)
THEN
	RESPONSE #100
	SetGlobal("_bHexVic","GLOBAL",5)
END

IF
	Global("_bHexVic","GLOBAL",5)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Viconia in Ust Natha. First Conversation with Imrae
IF
	IfValidForPartyDialog("Viconia")
	CombatCounter(0)
	See("Imrae")
	Global("_bViconiaTalksImrae1","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bViconiaTalksImrae1","GLOBAL",1)
END

IF
	Global("_bViconiaTalksImrae1","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Viconia in Ust Natha. First conversation about the city.
IF
	IfValidForPartyDialog("Viconia")
	GlobalGT("PhaereJob","GLOBAL",5) 
	AreaCheck("AR2200")
	CombatCounter(0)
	Global("_bViconiaTalksUstNatha1","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bViconiaTalksUstNatha1","GLOBAL",1)
END

IF
	Global("_bViconiaTalksUstNatha1","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Viconia in Ust Natha. Second conversation about city.
IF
	IfValidForPartyDialog("Viconia")
	CombatCounter(0)
	AreaCheck("AR2200")
	PartyHasItem("MISC9U") //Phaere's fake eggs
	OR(5) 
		!Dead("jael01") 
		!Dead("jael02") 
		!Dead("Jael03") 
		!Dead("udchal") 
		!Dead("Jarlich")
	Global("_bViconiaTalksUstNatha2","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bViconiaTalksUstNatha2","GLOBAL",1)
END

IF
	Global("_bViconiaTalksUstNatha2","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Viconia in Ust Natha. Third Conversation about City
IF
	IfValidForPartyDialog("Viconia")
	CombatCounter(0)
	PartyHasItem("MISC9V")
	Dead("jael01") 
	Dead("jael02") 
	Dead("Jael03") 
	Dead("udchal") 
	Dead("Jarlich")
	Global("_bViconiaTalksUstNatha3","GLOBAL",0)
	OR(2)
		AreaCheck("AR2200")
		AreaCheck("AR2201")
THEN
	RESPONSE #100
	SetGlobal("_bViconiaTalksUstNatha3","GLOBAL",1)
END

IF
	Global("_bViconiaTalksUstNatha3","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Viconia's talk about giving eggs to demon in Ust Natha
IF
	IfValidForPartyDialog("Viconia")
	CombatCounter(0)
	Global("_bCNWantsGiveEggsDemon","GLOBAL",1) 
	NumInPartyGT(2)
	Global("_bViconiaTalksGiveEggsDemon","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bViconiaTalksGiveEggsDemon","GLOBAL",1)
END

IF
	Global("_bViconiaTalksGiveEggsDemon","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Follow-up talks if you want to give the eggs to the demon
IF
	Global("_bCNWantsGiveEggsDemon","GLOBAL",1)
	IfValidForPartyDialog("Viconia")
	CombatCounter(0)
	Global("_bViconiaTalksGiveEggsDemon","GLOBAL",2)
THEN
	RESPONSE #100
	SetGlobal("_bViconiaTalksGiveEggsDemon","GLOBAL",3)
END

IF
	Global("_bViconiaTalksGiveEggsDemon","GLOBAL",3)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Creating Imrae and In-Temple talk after you have Phaere's fake eggs.
IF
	AreaCheck("AR2201") 
	Dead("jael01") 
	Dead("jael02") 
	Dead("Jael03") 
	Dead("udchal") 
	Dead("Jarlich") 
	IfValidForPartyDialogue("Viconia") 
	PartyHasItem("MISC9U") // Phaere's Fake Dragon Eggs
	Global("_bViconiaMeetsImraeInTemple","GLOBAL",0)
	Global("_bViconiaMeetsImraeOutside","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bViconiaMeetsImraeInTemple","GLOBAL",1)
	CreateCreature("UDIMRAE",[769.1683],SW)
END

IF
	Global("_bViconiaMeetsImraeInTemple","GLOBAL",1)
	Detect("Imrae")
THEN
	RESPONSE #100
	Dialogue("Imrae")
END

//Creating Imrae outside after you give eggs to Phaere. 
IF
	AreaCheck("AR2200") //Ust Natha
	Dead("jael01") 
	Dead("jael02") 
	Dead("Jael03") 
	Dead("udchal") 
	Dead("Jarlich") 
	IfValidForPartyDialogue("Viconia") 
	Global("PhaereJob","GLOBAL",11)
	Global("_bViconiaMeetsImraeInTemple","GLOBAL",0)
	Global("_bViconiaMeetsImraeOutside","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bViconiaMeetsImraeOutside","GLOBAL",1)
	CreateCreature("UDIMRAE",[2833.2175],W)
END

IF
	Global("_bViconiaMeetsImraeOutside","GLOBAL",1)
	Detect("Imrae")
THEN
	RESPONSE #100
	Dialogue("Imrae")
END

//Talk for Imrae to leave after talk if you're outside.
IF
	Global("_bViconiaMeetsImraeOutside","GLOBAL",2)
	AreaCheck("AR2200") //Ust Natha
	InMyArea("Imrae")
THEN
	RESPONSE #100
	ActionOverride("Imrae",EscapeArea())
END

//Next talk, if you're in the temple.
IF
	AreaCheck("AR2201") //Lolth Temple
	IfValidForPartyDialog("Viconia")
	Global("_bViconiaMeetsImraeInTemple","GLOBAL",2)
THEN
	RESPONSE #100
	SetGlobal("_bViconiaMeetsImraeInTemple","GLOBAL",3)
END

IF
	Global("_bViconiaMeetsImraeInTemple","GLOBAL",3)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//15 Day Timer for Viconia staying with Imrae
IF
	Global("_bViconiaStaysImraeShort","GLOBAL",2)
THEN
	RESPONSE #100
	SetGlobal("_bViconiaStaysImraeShort","GLOBAL",3)
	SetGlobalTimer("_bViconiaStaysWithImraeTimer","GLOBAL",FIFTEEN_DAYS)
	LeaveParty()
	SetGlobal("KickedOut","LOCALS",8)
	EscapeAreaMove("AR0011",635,1078,S)
END

//Dialogue in the temple after Viconia gives the eggs to the demon.
IF
	AreaCheck("AR2201")
	IfValidForPartyDialog("Viconia")
	GlobalGT("_bDemonWalks","GLOBAL",1)
	Global("_bViconiaShoutsDemonDevir","GLOBAL",0)
	OR(2)
		Detect("uddrow02")
		Detect("uddrow03")
THEN
	RESPONSE #100
	SetGlobal("_bViconiaShoutsDemonDevir","GLOBAL",1)
END

IF
	Global("_bViconiaShoutsDemonDevir","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Second Dialogue
IF
	AreaCheck("AR2200")
	IfValidForPartyDialog("Viconia")
	GlobalGT("_bDemonWalks","GLOBAL",2)
	Global("_bViconiaShoutsDemonDevir","GLOBAL",2)
THEN
	RESPONSE #100
	SetGlobal("_bViconiaShoutsDemonDevir","GLOBAL",3)
END

IF
	Global("_bViconiaShoutsDemonDevir","GLOBAL",3)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Dialogue at Exit Tunnels of Underdark with Demon
IF
	IfValidForPartyDialog("Viconia")
	Global("_bDemonWalks","GLOBAL",28)
	!Dead("uddemon")
	AreaCheck("AR2401")
THEN
	RESPONSE #100
	SetGlobal("_bDemonWalks","GLOBAL",29)
END

IF
	Global("_bDemonWalks","GLOBAL",29)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Timer to start second dialogue.
IF
	IfValidForPartyDialog("Viconia")
	Global("_bDemonWalks","GLOBAL",30)
	!Dead("uddemon")
THEN
	RESPONSE #100
	SetGlobal("_bDemonWalks","GLOBAL",31)
	SetGlobalTimer("_bExitTunnelsBattle","GLOBAL",ONE_ROUND)
END

IF
	IfValidForPartyDialog("Viconia")
	Global("_bDemonWalks","GLOBAL",31)
	!Dead("uddemon")
	GlobalTimerExpired("_bExitTunnelsBattle","GLOBAL")
THEN
	RESPONSE #100
	SetGlobal("_bDemonWalks","GLOBAL",32)
END

IF
	Global("_bDemonWalks","GLOBAL",32)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END


//Script for fighting elves outside.
IF
	Global("_bDemonWalks","GLOBAL",36)
	Global("_bDrowAttackElves","GLOBAL",1)
THEN
	RESPONSE #100
	SetGlobal("_bDrowAttackElves","GLOBAL",2)
	RestParty()
END

IF
	Global("_bDrowAttackElves","GLOBAL",2)
	!Dead("uddemon")
	IfValidForPartyDialogue("Viconia")
	Global("_bDemonWalks","GLOBAL",36)
THEN
	RESPONSE #100
	SetGlobal("_bDrowAttackElves","GLOBAL",3)
END

IF
	Global("_bDrowAttackElves","GLOBAL",3)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Script for Viconia's final talks if the Demon is dead.
IF
	GlobalGT("_bDemonWalks","GLOBAL",0)
	OR(2)
		Global("_bViconiaStaysImraeShort","GLOBAL",1)
		Global("_bViconiaStaysImrae","GLOBAL",1)
	Dead("uddemon")
	CombatCounter(0)
	Global("_bVicoinaLeavesForUstNatha","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bVicoinaLeavesForUstNatha","GLOBAL",1)
END

IF
	Global("_bVicoinaLeavesForUstNatha","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Script for Viconia's final talks if you get past the elven temple and go to a different area.
IF
	GlobalGT("_bDemonWalks","GLOBAL",0)
	!AreaCheck("AR2500") //Elven Temple Area
	OR(2)
		Global("_bViconiaStaysImraeShort","GLOBAL",1)
		Global("_bViconiaStaysImrae","GLOBAL",1)
	Global("_bVicoinaLeavesForUstNatha","GLOBAL",0)
	Global("Chapter","GLOBAL",%bg2_chapter_6%)
THEN
	RESPONSE #100
	SetGlobal("_bVicoinaLeavesForUstNatha","GLOBAL",1)
END

IF
	Global("_bVicoinaLeavesForUstNatha","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Script for final talks if combat is over and Demon is alive.
IF
	AreaCheck("AR2500") //Elven Temple Area
	IfValidForPartyDialog("Viconia")
	Global("_bVicoinaLeavesForUstNatha","GLOBAL",0)
	!InMyArea("c6elhan")
	Dead("c6gener")
	Dead("c6extr01")
	Dead("c6extr02")
	Dead("c6extr03")
	Dead("c6extr04")
	!InMyArea("warsage")
	Dead("c6elf1")
	Dead("c6elf4")
	Dead("c6elf5")
	!Detect([ENEMY])
THEN
	RESPONSE #100
	SetGlobal("_bVicoinaLeavesForUstNatha","GLOBAL",1)
END

IF
	Global("_bVicoinaLeavesForUstNatha","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END	

//After the demon, if Viconia goes to Ust Natha for 15 dayes (timer is on line 240)
IF
	Global("_bViconiaStaysImraeShort","GLOBAL",1)
	Global("_bVicoinaLeavesForUstNatha","GLOBAL",2)
THEN
	RESPONSE #100
	SetGlobal("_bViconiaStaysImraeShort","GLOBAL",2)
END

//After the demon, if Viconia goes to Ust Natha permanently.
IF
	Global("_bViconiaStaysImrae","GLOBAL",1)
	Global("_bVicoinaLeavesForUstNatha","GLOBAL",2)
THEN
	RESPONSE #100
	SetGlobal("KickedOut","LOCALS",1) 
	LeaveParty()
	EscapeArea()
END

//To trigger the last part of "The Fall of Two Houses" 
IF
	InParty("Viconia")
	OR(3)
		GlobalGT("_bViconiaStaysImraeShort","GLOBAL",0)
		GlobalGT("_bViconiaStaysImrae","GLOBAL",0)
		GlobalGT("_bViconiaNoStayImrae","GLOBAL",0)
	Global("_bFallofTwoHousesQuest","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bFallofTwoHousesQuest","GLOBAL",1)
	AddexperienceParty(50000)
	ChangeStat("Viconia",WIS,1,ADD)
	//Quest Complete: The Fall of Two Houses
	//Houses Jae'llat and Despana are no more and Imrae has taken control of the city. I am not sure what difference that makes in the world, but Viconia seems pleased.
END

//To talk when Viconia comes back. First part in baldur.bcs
IF
	Global("_bViconiaReturnsUstNatha","GLOBAL",1)
	Detect(Player1)
	!InPartyAllowDead("Viconia")
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Start of Viconia-Clara Genocide Quest
IF
	IfValidForPartyDialog("Viconia")
	IfValidForPartyDialog("_bClara")
	GlobalGT("_BBCLARAVIC","GLOBAL",4)
	Global("_bClaraRealizesMistakeViconia","GLOBAL",2)
	GlobalGT("Chapter","GLOBAL",%bg2_chapter_5%)
	Dead("c6bodhi")
	AreaType(OUTDOOR)
	Global("_bViconiaKillsAll","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobalTimer("_bVicKillsAllTimer","GLOBAL",ONE_HOUR)
	SetGlobal("_bViconiaKillsAll","GLOBAL",1)
END

IF
	IfValidForPartyDialog("Viconia")
	IfValidForPartyDialog("_bClara")
	GlobalTimerExpired("_bVicKillsAllTimer","GLOBAL")
	Global("_bViconiaKillsAll","GLOBAL",1)
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bViconiaKillsAll","GLOBAL",2)
END

IF
	Global("_bViconiaKillsAll","GLOBAL",2)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//After one week delay.
IF
	GlobalTimerExpired("_bGenocideDelayedOneWeek","GLOBAL")
	Global("_bViconiaKillsAll","GLOBAL",3)
	IfValidForPartyDialog("Viconia")
	IfValidForPartyDialog("_bClara")
	Global("_bGenocideWaitsAWeek","GLOBAL",0)
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bGenocideWaitsAWeek","GLOBAL",1)
END

IF
	Global("_bGenocideWaitsAWeek","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Viconia Genocide talk in Mazzy's house if friends with Mazzy.
IF
	Global("_bMazzyGenocideFamilyTalk","GLOBAL",2)
	IfValidForPartyDialog("Viconia")
	IfValidForPartyDialog("Mazzy")
	!Dead("vara")
	AreaCheck("AR2002") //Fentan Home
	OR(2) 
		Global("_bMazzyVicFriends","GLOBAL",1) 
		Global("_bMazzyVicFriends","GLOBAL",2)
THEN
	RESPONSE #100
	SetGlobal("_bMazzyGenocideFamilyTalk","GLOBAL",3)
END

IF
	Global("_bMazzyGenocideFamilyTalk","GLOBAL",3)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Final Viconia Genocide talk
IF
	IfValidForPartyDialog("Viconia")
	OR(2)
		Global("_bClaraKillsAllTrademeet","GLOBAL",2)
		Global("_bCNKillsAllTrademeet","GLOBAL",2)
	OR(2)
		Global("_bClaraKillsAllWindspear","GLOBAL",2)
		Global("_bCNKillsAllWindspear","GLOBAL",2)
	OR(2)
		Global("_bClaraKillsAllUmar","GLOBAL",2)
		Global("_bCNKillsAllUmar","GLOBAL",2)
	OR(2)
		Global("_bClaraKillsAllGovernment","GLOBAL",2)
		Global("_bCNKillsAllGovernment","GLOBAL",2)
	OR(2)
		Global("_bClaraKillsAllTempleDistrict","GLOBAL",2)
		Global("_bCNKillsAllTempleDistrict","GLOBAL",2)
	OR(2)
		Global("_bClaraKillsAllPromenade","GLOBAL",2)
		Global("_bCNKillsAllPromenade","GLOBAL",2)
	OR(2)
		Global("_bClaraKillsAllSlums","GLOBAL",2)
		Global("_bCNKillsAllSlums","GLOBAL",2)
	OR(2)
		Global("_bClaraKillsAllCityGates","GLOBAL",2)
		Global("_bCNKillsAllCityGates","GLOBAL",2)
	OR(2)
		Global("_bClaraKillsAllDocks","GLOBAL",2)
		Global("_bCNKillsAllDocks","GLOBAL",2)
	Global("_bViconiaGenocideFinalTalk","GLOBAL",0)
	AreaType(OUTDOOR)
	TimeOfDay(NIGHT)
THEN
	RESPONSE #100
	SetGlobal("_bViconiaGenocideFinalTalk","GLOBAL",1)
END

IF
	Global("_bViconiaGenocideFinalTalk","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

