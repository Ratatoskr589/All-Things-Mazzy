
//Imrae's Scripts in the Temple of Lolth to make all the egg guards go away.
IF
	Global("_bViconiaMeetsImraeInTemple","GLOBAL",4)
	OR(2)
		Detect(Player1)
		Detect("Viconia")
THEN
	RESPONSE #100
	SetGlobal("_bViconiaMeetsImraeInTemple","GLOBAL",5)
END

IF
	Global("_bViconiaMeetsImraeInTemple","GLOBAL",5)
THEN
	RESPONSE #100
	MoveToPointNoInterrupt([1244.1580])
	StartDialogNoSet(Player1) //Handmaiden of Lolth (uddrow36)
END

IF
	Global("_bViconiaMeetsImraeInTemple","GLOBAL",6)
	OR(2)
		Detect(Player1)
		Detect("Viconia")
THEN
	RESPONSE #100
	SetGlobal("_bViconiaMeetsImraeInTemple","GLOBAL",7)
END

IF
	Global("_bViconiaMeetsImraeInTemple","GLOBAL",7)
THEN
	RESPONSE #100
	MoveToPointNoInterrupt([1998.1400])
	StartDialogNoSet(Player1) //Matron's Guard (UDDROW01)
END

IF
	Global("_bViconiaMeetsImraeInTemple","GLOBAL",8)
	Global("_bImraeMakesGuardsLeave","GLOBAL",1) //Set in _bDWCITY.baf
	OR(2)
		Detect(Player1)
		Detect("Viconia")
THEN
	RESPONSE #100
	SetGlobal("_bViconiaMeetsImraeInTemple","GLOBAL",9)
END

IF
	Global("_bViconiaMeetsImraeInTemple","GLOBAL",9)
THEN
	RESPONSE #100
	MoveToPointNoInterrupt([2067.941])
	Dialog("UDDROW20")
END

IF
	Global("_bViconiaMeetsImraeInTemple","GLOBAL",10)
	!Detect("UDDROW20")
	!Detect("UDDROW21")
THEN
	RESPONSE #100
	SetGlobal("_bViconiaMeetsImraeInTemple","GLOBAL",11)
END

IF
	Global("_bViconiaMeetsImraeInTemple","GLOBAL",11)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

IF
	Global("_bViconiaMeetsImraeInTemple","GLOBAL",12)
THEN
	RESPONSE #100
	SetGlobal("_bViconiaMeetsImraeInTemple","GLOBAL",13)
	EscapeArea()
END

