IF //Script for if Mazzy goes hostile.
OR(2)
	Allegiance("Mazzy",ENEMY)
	AttackedBy([PC],DEFAULT)
!InParty("Mazzy")
Global("_bMazzyHostile","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bMazzyHostile","GLOBAL",2)
	Continue()
END


IF // Trigger for dialogue if Mazzy turns hostile.
Global("_bMazzyHostile","GLOBAL",2)
THEN
	RESPONSE #100
	ActionOverride("Mazzy",StartDialogNoSet(Player1))
	Continue()
END


IF //Once combat ends after Mazzy going hostile. Turns off everything. Script is here because Mazzy's dead.
Global("_bMazzyHostile","GLOBAL",4)
CombatCounter(0)
Dead("Mazzy")
THEN
	RESPONSE #100
	SetGlobal("_bMazzyHostile","GLOBAL",5)
	Continue()
END

IF //End of hostilities. Turn everyone selectable again. Script is here because being unselectable turns off script files.
	Global("_bMazzyHostile","GLOBAL",5)
	Global("_bMazzyFightOver","GLOBAL",0)
THEN
	RESPONSE #100
	ActionOverride("Jan",MakeUnselectable(0))
	ActionOverride("Cernd",MakeUnselectable(0))
	ActionOverride("NEERA",MakeUnselectable(0))
	ActionOverride("RASAAD",MakeUnselectable(0))
	ActionOverride("_bClara",MakeUnselectable(0))
	ActionOverride ("Viconia",MakeUnselectable(0))
	SetGlobal("_bmahojanfight","GLOBAL",1)
	SetGlobal("_bMazzyFightOver","GLOBAL",1)
	Continue()
END

IF //Korgan if Mazzy dies while he's hostile. Dialogue trigger in Korgan.baf
	Global("_bMazzyHostile","GLOBAL",4)
	Dead("Mazzy")
	!Dead("Korgan")
	Global("_bmahokorganfight","GLOBAL",1)
THEN
	RESPONSE #100
	ActionOverride("Korgan",JoinParty())
	ActionOverride("Korgan",ChangeEnemyAlly("Korgan",ALLY))
	ActionOverride("Korgan",ClearActions("Korgan"))
	SetGlobal("_bmahokorganfight","GLOBAL",2)
	Continue()
END


//Initial Hexxat Running, want to make sure she's moving before NPCs talk but can't escape
IF
	Global("_bDieHexxatDie","GLOBAL",1)
	Global("OHH_hexmove","OH7000",1)
	OR(9) 
		IsValidForPartyDialogue("Mazzy")
		IsValidForPartyDialogue("Keldorn")
		IsValidForPartyDialogue("Valygar")
		IsValidForPartyDialogue("Jan")
		IsValidForPartyDialogue("Minsc")
		IsValidForPartyDialogue("Jaheira")
		Global("_bAnoHatesHex","GLOBAL",1)
		IsValidForPartyDialogue("Nalia")
		IsValidForPartyDialogue("Aerie")
THEN
	RESPONSE #100
	Wait(5)
	SetGlobal("_bDieHexxatDie","GLOBAL",2)
	Continue()
END

IF
	Global("_bDieHexxatDie","GLOBAL",2)
THEN
	RESPONSE #100
	ActionOverride(Player1,StartDialogue("player1",Player1))
END
